import groovy.json.JsonSlurper

plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.10.0'
    id 'com.google.cloud.tools.jib' version '3.5.1'
}

group = 'net.neoforged'
version = '0.0.1-SNAPSHOT'
description = 'Meta API for NeoForge'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

final dockerMetadataJson = providers.environmentVariable("DOCKER_METADATA_JSON").map {
    final slurper = new JsonSlurper()
    return slurper.parseText(it)
}
jib {
    from {
        image = project.docker_base_image
    }
    to {
        image = 'ghcr.io/neoforged/metaapi'
        tags = dockerMetadataJson.map { new HashSet<String>(it['tag-names']) }
        auth {
            username = providers.environmentVariable("GH_USERNAME")
            password = providers.environmentVariable("GH_PASSWORD")
        }
    }
    container {
        ports = ['8080']
        jvmFlags = ['--enable-native-access=ALL-UNNAMED']
        labels.putAll(dockerMetadataJson.map { it['labels'] as Map })
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-freemarker'
	implementation 'org.springframework.boot:spring-boot-flyway'
    implementation 'tools.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.commonmark:commonmark:0.27.0'
    implementation 'org.apache.maven:maven-artifact:4.0.0-rc-5'

	// https://mvnrepository.com/artifact/org.webjars/font-awesome
	runtimeOnly 'org.webjars:font-awesome:7.1.0'
	runtimeOnly 'org.webjars.npm:jquery:3.7.1'
	runtimeOnly 'org.webjars.npm:datatables.net-dt:2.3.5'
	runtimeOnly 'org.webjars.npm:datatables.net-columncontrol-dt:1.1.1'
	runtimeOnly 'org.webjars.npm:datatables.net-fixedheader-dt:4.0.5'

	implementation 'org.flywaydb:flyway-core'

	runtimeOnly 'org.xerial:sqlite-jdbc:3.47.1.0'
	runtimeOnly 'org.hibernate.orm:hibernate-community-dialects:7.0.0.Beta1'

	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// OpenAPI Generator configuration
openApiGenerate {
	generatorName = 'spring'
	inputSpec = layout.projectDirectory.file('src/main/resources/static/openapi.yaml').asFile.toURI().toString()
	outputDir = layout.buildDirectory.dir('generated').get().asFile.absolutePath
	apiPackage = 'net.neoforged.meta.generated.api'
	modelPackage = 'net.neoforged.meta.generated.model'
	configOptions = [
            annotationLibrary: 'none',
			documentationProvider: 'none',
			skipDefaultInterface: 'true',
			interfaceOnly: 'true',
            openApiNullable: 'false',
            useSpringBoot3: 'true',
            useSwaggerUI: 'false',
            requestMappingMode: 'api_interface'
	]
}

// Add generated sources to source sets
sourceSets {
	main {
		java {
			srcDir layout.buildDirectory.dir('generated/src/main/java')
		}
	}
}

// Ensure code generation runs before compilation
tasks.named('compileJava') {
	dependsOn tasks.openApiGenerate
}

tasks.named('test', Test) {
	useJUnitPlatform()
    jvmArgs("--enable-native-access=ALL-UNNAMED")
}
